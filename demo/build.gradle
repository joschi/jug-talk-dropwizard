plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id "net.ltgt.apt" version "0.9"
    id "nebula.dependency-recommender" version "4.1.0"
}

version = '1.0.0-SNAPSHOT'
group = 'com.example.demo'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// The main class of the application
mainClassName = 'com.example.demo.DemoApplication'

// Disable building ZIP and TAR archives with Gradle Application plugin
distTar.enabled = false
distZip.enabled = false

repositories {
    jcenter()
}

ext {
    dropwizardVersion = '1.1.0-rc4'
    immutablesVersion = '2.4.4'
}

dependencyRecommendations {
   mavenBom module: "io.dropwizard:dropwizard-bom:${dropwizardVersion}"
}

dependencies {
    compile "io.dropwizard:dropwizard-core"
    compile "io.dropwizard:dropwizard-assets"
    compile "io.dropwizard:dropwizard-views"
    compile "io.dropwizard:dropwizard-views-mustache"

    // Community project: https://github.com/smoketurner/dropwizard-swagger
    compile "com.smoketurner:dropwizard-swagger:1.0.6-1"

    // Immutables: https://immutables.github.io/
    apt "org.immutables:value:${immutablesVersion}"
    compileOnly "org.immutables:value:${immutablesVersion}:annotations"

    testCompile "io.dropwizard:dropwizard-testing"
    testCompile "org.mockito:mockito-core"
}

// Configure the run task to start the Dropwizard application
run {
    args 'server', 'config.yml'
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}
