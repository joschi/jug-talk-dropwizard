plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id "org.inferred.processors" version "1.2.3"
}

version = '1.0.0-SNAPSHOT'
group = 'com.example.demo'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// The main class of the application
mainClassName = 'com.example.demo.DemoApplication'

// Disable building ZIP and TAR archives with Gradle Application plugin
distTar.enabled = false
distZip.enabled = false

repositories {
    jcenter()
}

ext {
    dropwizardVersion = '1.1.0-rc3'
}

dependencies {
    compile "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-assets:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-views:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-views-mustache:${dropwizardVersion}"

    // Community project: https://github.com/smoketurner/dropwizard-swagger
    compile "com.smoketurner:dropwizard-swagger:1.0.6-1"

    // Immutables: https://immutables.github.io/
    processor 'org.immutables:value:2.4.0'

    testCompile "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
    testCompile 'org.mockito:mockito-core:2.7.17'
}

// Configure the run task to start the Dropwizard application
run {
    args 'server', 'config.yml'
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}
